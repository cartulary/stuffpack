use="all"

#Set some useful variables
HOST="$(hostname)"
HOST="${HOST%%.*}"

source ~/.conf/env.sh
source ~/.conf/aliases.sh
source ~/.conf/zsh.load.zsh
source ~/.conf/zsh.alias.zsh
source ~/.conf/zsh.complete.zsh
source ~/.conf/ps1.zsh
source ~/.conf/home-end.zsh
source ~/.conf/eval_spin.func

fpath=( $fpath ~/bin/zsh-bin )

umask 0022;

#To discover what keycode is being sent, hit ^v
#and then the key you want to test.

setopt auto_pushd
setopt pushd_ignore_dups
setopt complete_aliases
setopt hist_expire_dups_first
setopt hist_reduce_blanks
setopt hist_verify
#unsetopt check_jobs #don't warn me when I have jobs in the background
unsetopt hup #when the shell exits keep background jobs.
#setopt bsd_echo # make zsh echo act like bsd echo

# title/precmd/postcmd
function precmd() {
	setCurrentPS1
	title "zsh - $PWD"
}

function preexec() {
  # The full command line comes in as "$1"
  local cmd="$1"
  local -a args

  # add '--' in case $1 is only one word to work around a bug in ${(z)foo}
  # in zsh 4.3.9.
  tmpcmd="$1 --"
  args=${(z)tmpcmd}

  # remove the '--' we added as a bug workaround..
  # per zsh manpages, removing an element means assigning ()
  args[${#args}]=()
  if [ "${args[1]}" = "fg" ] ; then
    local jobnum="${args[2]}"
    if [ -z "$jobnum" ] ; then
      # If no jobnum specified, find the current job.
      for i in ${(k)jobtexts}; do
        [ -z "${jobstates[$i]%%*:+:*}" ] && jobnum=$i
      done
    fi
    cmd="${jobtexts[${jobnum#%}]}"
  else
  fi
  title "$cmd"
}
function title() {
  # This is madness.
  # We replace literal '%' with '%%'
  # Also use ${(V) ...} to make nonvisible chars printable (think cat -v)
  # Replace newlines with '; '
  local value="${${${(V)1//\%/\%\%}//'\n'/; }//'\t'/ }"
  local location

  location="$HOST"
  [ "$USERNAME" != "$LOGNAME" ] && location="${USERNAME}@${location}"

  # Special format for use with print -Pn
  value="%70>...>$value%<<"
  unset PROMPT_SUBST
  case $TERM in
    screen)
      # Put this in your .screenrc:
      # hardstatus string "[%n] %h - %t"
      # termcapinfo xterm 'hs:ts=\E]2;:fs=\007:ds=\E]2;screen (not title yet)\007'
      print -Pn "\ek${value}\e\\"     # screen title (in windowlist)
      print -Pn "\e_${location}\e\\"  # screen location
      ;;
    xterm*)
      print -Pn "\e]0;$value\a"
      ;;
    Eterm*)
		print -Pn "\e]2;$value\a\e[1;\a";;
  esac
  setopt LOCAL_OPTIONS
}

# Lines configured by zsh-newuser-install
HISTFILE=~/.zsh_history
HISTSIZE=10100
SAVEHIST=10000
setopt appendhistory extendedglob
setopt autocd
#setopt share_history
setopt hist_verify
setopt inc_append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_space
setopt correct_all

#setopt SHARE_HISTORY
setopt APPEND_HISTORY
unsetopt nomatch
bindkey -e
# End of lines configured by zsh-newuser-install
#cd
